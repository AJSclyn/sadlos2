apply plugin: 'org.xtext.xtend'

if (name.endsWith(".tests")) {
	sourceSets {
		main {
			java.srcDirs = []
			resources.srcDirs = []
		}
		test {
			java.srcDirs = ['src', 'src-gen']
			resources.srcDirs = ['src', 'src-gen']
			xtendOutputDir = 'xtend-gen'
		}
	}
} else if (name.endsWith(".monaco")) {
	sourceSets {
		main {
			java.srcDirs = ['src/main/java', 'src/main/xtext-gen']
			resources.srcDirs = ['src/main/resources', 'src/main/xtext-gen']
			xtendOutputDir = 'src/main/xtend-gen'
		}
		test {
			java.srcDirs = ['src/test/java', 'src/test/xtext-gen']
			resources.srcDirs = ['src/test/resources', 'src/test/xtext-gen']
			xtendOutputDir = 'src/test/xtend-gen'
		}
	}
} else {
	sourceSets {
		main {
			java.srcDirs = ['src', 'src-gen']
			resources.srcDirs = ['src', 'src-gen']
			xtendOutputDir = 'xtend-gen'
		}
		test {
			java.srcDirs = []
			resources.srcDirs = []
		}
	}
}

sourceSets.all {
	resources.exclude '**/*.g', '**/*.xtext', '**/*.mwe2', '**/*.xtend', '**/*._trace'
}

jar {
	from('model') {
		into('model')
	}
	manifest {
		attributes 'Bundle-SymbolicName': project.name
	}
}

plugins.withId('war') {
	webAppDirName = "WebRoot"
}

plugins.withId('org.xtext.idea-plugin') {
	assembleSandbox.metaInf.from('META-INF')
}
